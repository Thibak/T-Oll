/***********************************************************************************************************/
/***********************************************************************************************************/
/***********************************************************************************************************/
/***********************************************************************************************************/
/*****************                                                                       *******************/
/****************                 Программа чтения протокола ОЛЛ-2009                     ******************/
/*****************                                                                       *******************/
/***********************************************************************************************************/
/***********************************************************************************************************/
/***********************************************************************************************************/
/***********************************************************************************************************/
/*идентификатор компа*/ *D - sony, Z - ГНЦ;
%macro what_OC;
%if &sysscpl = W32_7PRO %then 
	%do;
		%let disk = D; *sony;
	%end;
%else/*%if &sysscpl = "W32_7PRO" %then */ 
	%do;
		%let disk = Z; *остальные;
	%end;
%mend;


/*определитель ОС*/
/*data comp;*/
/*	OC = "&sysscpl";*/
/*run;*/
/**/
/*proc print data = COMP;*/
/*run;*/
%what_OC;

%let dirURI = &disk.:\AC\OLL-2009\cur_base\; * директория с базой;
%let metaFN = meta.txt; * имя файла метаданных;
%let LN = ALL2009; * имя библиотеки;
%let lbs = labels.txt; * файл с лейблами (для всех таблиц в кучу);

Libname &LN "&disk.:\AC\OLL-2009\SAS"; * создали файл данных;
filename tranfile "&disk.:\AC\OLL-2009\REG_ARCH_&sysdate9..stc"; * условный идентификатор транспортного файла;

/***********************************************************************************************************/
/************************************* формат фала метаданных **********************************************/
/**                                                                                                       **/
/**    имя файла ***** табуляция ***** имя датасета                                                       **/
/**                                                                                                       **/
/***********************************************************************************************************/

OPTIONS CTRYDECIMALSEPARATOR=',' ;

*Импортируем файл метаданных;
proc import datafile="&dirURI&metaFN" dbms=tab out=&LN..TABLES replace; 
	*delimiter='09'x; * разделитель -- '09'x табуляция;
	getnames=yes; * получать имена переменных?;
run; 

*Макрос импорта данных из таб-делиметед файлов;
%macro imprt (src, dst);
	proc import datafile="&dirURI&src" dbms=csv out=&LN..&dst replace; 
		*delimiter='09'x; * разделитель -- '09'x табуляция;
		delimiter=';';
		getnames=yes; * получать имена переменных?;
		guessingrows=500;  * как много строк нужно просканировать, для определения максемальной длинны поля;
	run; 
%mend imprt;


%macro lbls (tn);
data &LN..&tn;
	SET &LN..&tn;
%include "&dirURI&lbs";
run;
%mend;

*для каждой записи метафайла выполняем макрос экспорта;
data _Null_; * Не создаем ни какого датасета;
	set &LN..TABLES; * для работы берем таблицу метаданных;
	CALL execute ('%imprt('||FN||','||TN||')'); * выполнить макрос экспорта;
	CALL execute ('%lbls('||TN||')'); * выполнить макрос присвоения лейбалов;
run; 

/*ЗАПЛАТКА!!! Подгружаем данные по возрастам*/
proc import datafile="&dirURI.age.txt" dbms=tab out=&LN..tmp_AGE replace; 
		getnames=yes; * получать имена переменных?;
		guessingrows=500;  * как много строк нужно просканировать, для определения максемальной длинны поля;
run; 

*загоняем в транспортный файл;
proc cport library=&LN file=tranfile;
run;

